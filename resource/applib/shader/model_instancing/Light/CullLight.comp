#version 450
#extension GL_GOOGLE_cpp_style_line_directive : require


#define SETPOINT_CAMERA 1
#include <btrlib/Camera.glsl>

#define USE_LIGHT 0
#include <applib/Light.glsl>

void main()
{
	uvec3 index = uvec3(gl_GlobalInvocationID.xy, 0);
	if(any(greaterThanEqual(index.xy, u_light_info.m_tile_num))) { return; }
	uint index1D = index.x + index.y * u_light_info.m_tile_num.x;

	CameraFrustomPoint frustom_point = u_camera_frustom_point[0];
	vec3 n_per_size = vec3(vec2(1.) / u_light_info.m_tile_num, 0.) * (frustom_point.m_rbn - frustom_point.m_ltn).xyz;
	vec3 f_per_size = vec3(vec2(1.) / u_light_info.m_tile_num, 0.) * (frustom_point.m_rbf - frustom_point.m_ltf).xyz;
	vec3 ltn = frustom_point.m_ltn.xyz + index*n_per_size;
	vec3 lbn = frustom_point.m_ltn.xyz + (index+uvec3(0, 1, 0))*n_per_size;
	vec3 rtn = frustom_point.m_ltn.xyz + (index+uvec3(1, 0, 0))*n_per_size;
	vec3 rbn = frustom_point.m_ltn.xyz + (index+uvec3(1, 1, 0))*n_per_size;
	vec3 ltf = frustom_point.m_ltf.xyz + index*f_per_size;
	vec3 lbf = frustom_point.m_ltf.xyz + (index+uvec3(0, 1, 0))*f_per_size;
	vec3 rtf = frustom_point.m_ltf.xyz + (index+uvec3(1, 0, 0))*f_per_size;
	vec3 rbf = frustom_point.m_ltf.xyz + (index+uvec3(1, 1, 0))*f_per_size;
	CameraFrustom f;
	f.p[0] = MakePlane(rtn, ltn, ltf);
	f.p[1] = MakePlane(lbn, rbn, rbf);
	f.p[2] = MakePlane(ltn, lbn, lbf);
	f.p[3] = MakePlane(rbn, rtn, rbf);

	uint head = -1;
	for(uint i = 0; i < u_light_info.m_active_light_num; i++)
	{
		if(isCullingInf(f, b_light[i].m_position*vec4(1., 1., 1., 2.))) {
			continue;
		}
		uint light_index = atomicAdd(b_light_count, 1);
		b_light_LL[light_index].next = head;
		b_light_LL[light_index].light_index = i;
		head = light_index;
	}
	b_light_LL_head[index1D] = head;
}

