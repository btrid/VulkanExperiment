#version 450
#extension GL_GOOGLE_include_directive : require

#include "btrlib/ConvertDimension.glsl"
#include "btrlib/Common.glsl"
#include "btrlib/Math.glsl"

#define USE_GI2D 0
#define USE_GI2D_Radiosity 1
#include "GI2D.glsl"


layout (local_size_x = 256, local_size_y = 1) in;
void main()
{
	const ivec4 reso = ivec4(u_gi2d_info.m_resolution.xy, u_gi2d_info.m_resolution.xy/8);

	float angle = 6.28 / gl_NumWorkGroups.y;
	angle += angle * (gl_GlobalInvocationID.z / gl_NumWorkGroups.z);
	vec2 dir = rotate(angle * gl_GlobalInvocationID.y);
//	dir.x = abs(dir.x) < 0.0001 ? 0.0001 : dir.x;
//	dir.y = abs(dir.y) < 0.0001 ? 0.0001 : dir.y;
//	vec2 inv_dir = abs(1./dir);
//	dir *= min(inv_dir.x, inv_dir.y);

	vec2 begin;
	vec2 end;
	{
		vec2 floorp;
		floorp.x = dir.x >= 0 ? 0. : reso.x;
		floorp.y = dir.y >= 0 ? 0. : reso.y;
		vec2 floordir;
		floordir.x = abs(dir.x) > abs(dir.y) ? 0. : 1.;
		floordir.y = abs(dir.x) > abs(dir.y) ? 1. : 0.;

		vec2 p0 = intersectRayRay(vec2(0, 0), dir, floorp, floordir);
		vec2 p1 = intersectRayRay(vec2(reso.x, 0), dir, floorp, floordir);
		vec2 p2 = intersectRayRay(vec2(0, reso.y), dir, floorp, floordir);
		vec2 p3 = intersectRayRay(vec2(reso.x, reso.y), dir, floorp, floordir);

		vec2 minp = min(min(min(p0, p1), p2), p3);
		begin = minp + floordir * (8.) * gl_LocalInvocationIndex;
		if(!marchToAABB(begin, dir, vec2(0.), reso.xy)){ return; }
	}
	{
		vec2 inv_floorp;
		inv_floorp.x = dir.x < 0 ? 0. : reso.x;
		inv_floorp.y = dir.y < 0 ? 0. : reso.y;
		vec2 inv_floordir;
		inv_floordir.x = abs(dir.x) > abs(dir.y) ? 0. : 1.;
		inv_floordir.y = abs(dir.x) > abs(dir.y) ? 1. : 0.;

		vec2 p0 = intersectRayRay(vec2(0, 0), -dir, inv_floorp, inv_floordir);
		vec2 p1 = intersectRayRay(vec2(reso.x, 0), -dir, inv_floorp, inv_floordir);
		vec2 p2 = intersectRayRay(vec2(0, reso.y), -dir, inv_floorp, inv_floordir);
		vec2 p3 = intersectRayRay(vec2(reso.x, reso.y), -dir, inv_floorp, inv_floordir);

		vec2 maxp = min(min(min(p0, p1), p2), p3);
		end = maxp + inv_floordir * (8.) * gl_LocalInvocationIndex;
		if(!marchToAABB(end, -dir, vec2(0.), reso.xy)){ return; }
	}

	float progress = distance(begin, end);
	D2Ray ray;
	ray.origin = begin;
	ray.dir = dir * progress;

	int index = atomicAdd(b_ray_counter.w, 1);
	if((index % 512) == 0)
	{
		atomicAdd(b_ray_counter.x, 1);
	}
	b_ray[index] = ray;
}
