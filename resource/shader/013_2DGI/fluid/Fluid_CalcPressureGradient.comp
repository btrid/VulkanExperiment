#version 460
#extension GL_GOOGLE_include_directive : require

#include "btrlib/ConvertDimension.glsl"
#include "btrlib/Shape.glsl"

#define USE_Fluid2D 0
#define USE_GI2D 1
#include "GI2D.glsl"
#include "Fluid2D.glsl"

layout (local_size_x = 1024) in;

void main() 
{
	int p_id = int(gl_GlobalInvocationID.x);
	const vec2 p_pos = b_pos[p_id]*Scale;
	vec2 index_f = p_pos / Grid_Size;
	ivec2 index = ivec2(index_f);
	ivec2 reso = ivec2(u_gi2d_info.m_resolution/Grid_Size);
	if(any(lessThan(index, ivec2(0))) || any(greaterThanEqual(index, reso))){ return; }

	ivec3 neighbor = ivec3(ivec2(greaterThan(index_f, vec2(Grid_Size)*0.5)) * 2 - 1, 0);
	ivec4 access_list_x = (index.xxxx + neighbor.zzxx);
	ivec4 access_list_y = (index.yyyy + neighbor.zyzy);
	ivec4 access_list = access_list_x + access_list_y * reso.x;
	bvec4 is_wall = getWall(ivec2(p_pos));

	vec2 acc = vec2(0.);
	const float min_pressure = b_minimum_pressure[p_id];
	float pressure = b_pressure[p_id];
	for(int i = 0; i < 4; i++)
	{
		for(int target_id = b_grid_head[access_list[i]]; target_id != -1; target_id = b_grid_node[target_id])
		{
			if(p_id == target_id)
			{
				// 自分とは判定しない
				continue; 
			}
			const vec2 t_pos = b_pos[target_id]*Scale;

			float dist = distance(p_pos, t_pos);
			if(dist < InfluenceRadius) 
			{
				float w = calcWeight(dist, InfluenceRadius);
				w *= (b_pressure[target_id] - min_pressure);
				acc -= normalize(t_pos - p_pos)*w;
			}
		}
	}

	// 壁のほうが絶対強い
	float p = length(acc);
	for(int i = 0; i < 4; i++)
	{
		if(is_wall[i])
		{
			vec2 w_pos = vec2(access_list_x[i], access_list_y[i]) + vec2(0.5);
			float dist = distance(p_pos, w_pos);
			if(dist < InfluenceRadius) 
			{
				float w = calcWeight(dist, InfluenceRadius);
				w *= (pressure);
				acc -= normalize(w_pos-p_pos)*(p+1);
			}

		}
	}


	float p2 = length(acc);
	p2 = min(p2, 3.);
	b_acc[p_id] = p2 == 0. ? vec2(0.) : (normalize(acc) * p2);

}