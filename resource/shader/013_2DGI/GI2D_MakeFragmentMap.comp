#version 450
#extension GL_GOOGLE_include_directive : require

#include "btrlib/ConvertDimension.glsl"
#include "btrlib/Common.glsl"
#include "btrlib/Math.glsl"

#define USE_GI2D 0
#include "GI2D.glsl"

layout (local_size_x = 32, local_size_y = 32) in;

shared uint s_bit[16][2];
shared uint s_light_bit[16][2];
void main()
{
//	ivec2 id = ivec2(gl_GlobalInvocationID.xy) + ivec2(u_gi2d_scene.m_frame%2,u_gi2d_scene.m_frame/2) - ivec2(1);
	ivec2 id = ivec2(gl_GlobalInvocationID.xy);
	id = clamp(id, ivec2(0), u_gi2d_info.m_resolution);

	int tiled_1D_index = id.x + id.y*u_gi2d_info.m_resolution.x; 
	vec3 albedo = b_fragment[tiled_1D_index].albedo.xyz;
	float emissive = b_fragment[tiled_1D_index].albedo.w;
	bool is_fragment = (emissive <= 0.0) &&  dot(albedo, albedo) >= 0.01;
	bool is_emissive = (emissive >= 1.);

	uint power = packEmissive(albedo*LightPower);
	b_light[getMemoryOrder(ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y))] = is_emissive ? power : 0;

	b_jfa[tiled_1D_index].nearest_index = is_fragment ? id : ivec2(-1);
	b_jfa[tiled_1D_index].distance = is_fragment ? 0. : 99999999.;
	b_jfa[tiled_1D_index].e_nearest_index = is_emissive ? id : ivec2(-1);
	b_jfa[tiled_1D_index].e_distance = is_emissive ? 0. : 99999999.;

	// fragment map
	{
		uvec2 local = gl_LocalInvocationID.xy / 8;
		uint index = local.x+local.y*4;
		uvec2 localsub = gl_LocalInvocationID.xy % 8;
		if(all(equal(localsub, uvec2(0))))
		{
			s_bit[index][0] = 0;
			s_bit[index][1] = 0;
			s_light_bit[index][0] = 0;
			s_light_bit[index][1] = 0;
		}
		barrier();
		memoryBarrierShared();

		uint bit_offset = localsub.x + localsub.y*8;
		uint hilow = bit_offset/32;
		uint offset = bit_offset%32;
		is_fragment ? atomicOr(s_bit[index][hilow], 1<<offset) : 0;
		is_emissive ? atomicOr(s_light_bit[index][hilow], 1<<offset) : 0;

		barrier();
		memoryBarrierShared();

		if(all(equal(localsub, uvec2(0))))
		{
			uvec2 global = gl_GlobalInvocationID.xy/8; 
			uint store = global.x + global.y*u_gi2d_info.m_resolution.x/8;

			uint64_t bit =  uint64_t(s_bit[index][1]) << 32 | s_bit[index][0];
			uint64_t light_bit =  uint64_t(s_light_bit[index][1]) << 32 | s_light_bit[index][0];

			b_fragment_map[store] = u64vec2(bit, light_bit);
		}

	}
}

