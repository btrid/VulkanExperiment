#version 450

#extension GL_GOOGLE_cpp_style_line_directive : require

#include <btrlib/ConvertDimension.glsl>
#include <btrlib/Common.glsl>
#include <btrlib/Math.glsl>

#define USE_PM 0
#include <PM.glsl>

layout(push_constant) uniform BounceInfo
{
	bool is_store_photon;
	int bounce_count;
} constant;

layout (local_size_x = 32, local_size_y = 32) in;
shared Emission s_emission[64];
shared ivec2 s_emission_pos[64];
void main()
{
	const vec2 default_cell_size = vec2(1.);
	const ivec4 reso = ivec4(u_pm_info.m_resolution.xy, u_pm_info.m_resolution.xy/8);
	const ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);

	const vec2 end = vec2(pixel) + 0.5;
	const ivec2 iend = ivec2(end);
	vec3 photon = vec3(0.);
	const ivec2 tile_index_2d = pixel / ivec2(u_pm_info.m_emission_tile_size.xy);
	const int tile_index = int(tile_index_2d.x + tile_index_2d.y*u_pm_info.m_emission_tile_num.x);
	int emission_num = min(b_emission_tile_counter[tile_index.x], 64);
	{
		if(gl_LocalInvocationIndex < emission_num)
		{
			int emission_index = b_emission_tile_map[tile_index*u_pm_info.m_emission_tile_map_max + gl_LocalInvocationIndex];
			s_emission[gl_LocalInvocationIndex] = b_emission[emission_index];
			s_emission_pos[gl_LocalInvocationIndex] = ivec2(emission_index%u_pm_info.m_resolution.x, emission_index/u_pm_info.m_resolution.x);
		}
		barrier();
		memoryBarrierShared();
	}

	{
		ivec2 fragment_index = iend/8;
		int fragment_index_1d = fragment_index.x + fragment_index.y*reso.z;
		uint64_t fragment_map = b_fragment_map[fragment_index_1d];

		ivec2 fragment_index_sub = iend%8;
		int fragment_index_sub_1d = fragment_index_sub.x + fragment_index_sub.y*8;
		uint64_t bit = 1ul<<fragment_index_sub_1d;

		if((fragment_map & bit) != 0)
		{ 
			// kabe
			photon = vec3(1., 0., 0.);
			emission_num = 0;
		}
	}

	for(int i = 0; i < emission_num; i++)
	{
		vec4 emission = s_emission[i].value;
		float emissive_power = length(emission.xyz);
		vec2 emission_pos = vec2(s_emission_pos[i]) + 0.5;
		const vec2 start = emission_pos.xy;

		const vec2 diff = (end - start);
		if(dot(diff, diff) <= 10.1)
		{
			// ライト
			photon = vec3(0., 1., 0.);
			break;
		}
		const float ray_dist = length(diff.xy);

		vec2 dir = normalize(diff);
		dir.x = abs(dir.x) <= FLT_EPSIRON ? FLT_EPSIRON*2. : dir.x;
		dir.y = abs(dir.y) <= FLT_EPSIRON ? FLT_EPSIRON*2. : dir.y;

		const ivec3 next_step = ivec3((dir.x < 0. ? -1 : 1), (dir.y < 0. ? -1 : 1), 0);

		vec2 pos = start;
		ivec2 map_index = ivec2(pos / default_cell_size.xy);
		const ivec2 istart = ivec2(emission_pos.xy);
		const ivec2 target_dist = abs(istart-iend);


		for(;;)
		{
			int hierarchy=7;
			{
				for(; hierarchy>0; hierarchy--)
				{
					ivec2 findex2d = map_index>>hierarchy;
					int findex = findex2d.x + findex2d.y*(u_pm_info.m_resolution.x>>hierarchy);
					int offset = getFragmentHierarchyOffset(hierarchy);
					if(b_fragment_hierarchy[findex + offset] == 0)
					{
						break;
					}
				}
			}
			// march
			{
				vec2 cell_size = default_cell_size*(1<<hierarchy);
				vec2 cell_origin = vec2(map_index>>hierarchy)*cell_size;
				vec2 cell_p = pos - cell_origin;

				float x = dir.x < 0. ? cell_p.x : (cell_size.x- cell_p.x);
				float y = dir.y < 0. ? cell_p.y : (cell_size.y- cell_p.y);

				x = x <= FLT_EPSIRON ? (x+cell_size.x) : x;
				y = y <= FLT_EPSIRON ? (y+cell_size.y) : y;

				vec2 dist = abs(vec2(x, y) / dir);
				int next_ = dist.x < dist.y ? 0 : 1;
				pos += dir * dist[next_];

				ivec2 next = next_ == 0 ? next_step.xz : next_step.zy;
				map_index = ivec2(pos+ next*0.5);

			}

			// 終了判定
			if(all(greaterThanEqual(abs(ivec2(map_index)-istart), target_dist)))
			{
				// 距離を超えたら光がフラグメントにヒット
				photon += vec3(0., 0., 1.) * emissive_power / (1+ray_dist.x*ray_dist.x);
				break;
			}

			// hit確認
			{
				ivec2 fragment_index = map_index/8;
				int fragment_index_1d = fragment_index.x + fragment_index.y*reso.z;
				uint64_t fragment_map = b_fragment_map[fragment_index_1d];

				ivec2 fragment_index_sub = map_index%8;
				int fragment_index_sub_1d = fragment_index_sub.x + fragment_index_sub.y*8;
				uint64_t bit = 1ul<<fragment_index_sub_1d;

				if((fragment_map & bit) != 0)
				{ 
					// バウンス
					if(constant.is_store_photon)
					{
						int store_shift = constant.bounce_count+1;
						int photon_offset = u_pm_info.m_emission_buffer_offset[store_shift];
	
						ivec2 store_index_2d = map_index>>store_shift;
						int store_index = store_index_2d.x + store_index_2d.y*(u_pm_info.m_resolution.x>>store_shift);
						if(atomicCompSwap(b_emission_map[photon_offset + store_index], -1, 0) == -1)
						{
							int list_index = atomicAdd(b_emission_counter[store_shift].x, 1);
							b_emission_list[photon_offset+list_index] = store_index;
							b_emission[photon_offset+store_index].value = vec4(0.);// 危ないかも？
						}
						b_emission[photon_offset+store_index].value += vec4(1., 0., 0., 0.) * emissive_power / (1+ray_dist.x*ray_dist.x);
					}

					break;
				}
			}
		}
	}
	const int pixel_1d = pixel.x + pixel.y*u_pm_info.m_resolution.x;
	b_color[pixel_1d] = vec4(photon, 1.);
}
