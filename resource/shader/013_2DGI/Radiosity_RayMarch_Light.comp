#version 450
#extension GL_GOOGLE_include_directive : require

#include "btrlib/ConvertDimension.glsl"
#include "btrlib/Common.glsl"
#include "btrlib/Math.glsl"

#define USE_GI2D 0
#define USE_GI2D_Radiosity 1
#include "GI2D.glsl"

layout (local_size_x=1024) in;
void main()
{
	if(gl_GlobalInvocationID.x >= b_segment_counter.w){ return; }

	const int hierarchy = u_gi2d_scene.m_hierarchy;
	const ivec4 reso = ivec4(u_gi2d_info.m_resolution.xy, u_gi2d_info.m_resolution.xy/8);

	D2Segment seg = b_segment[gl_GlobalInvocationID.x];
	D2Ray ray = b_ray[seg.ray_index];

	vec2 dir = rotate(ray.angle);
	vec2 inv_dir;
	inv_dir.x = dir.x == 0. ? 99999999. : abs(1./dir.x);
	inv_dir.y = dir.y == 0. ? 99999999. : abs(1./dir.y);
	float min_march = min(inv_dir.x, inv_dir.y);

	vec2 pos = ray.origin + dir*seg.begin;
	ivec2 map_index = ivec2(pos);


	seg.radiance = 0;
	float progress = 0.;
	for(;progress < seg.march;)
	{
		float d = b_sdf[map_index.x + map_index.y*reso.x].y;
		{
			if(d <= 0.1)
			{
				seg.radiance += b_light[getMemoryOrder(map_index)];
				d = min_march;
			}
		}

		// march
		{
			// DDA
			progress += d;
			pos = ray.origin + dir*(seg.begin+progress);
			map_index = ivec2(pos);
		}
	}

	if(seg.radiance != 0)
	{
		int index = atomicAdd(b_photon_counter.w, 1);
		if(index%1024==0){
			atomicAdd(b_photon_counter.x, 1);
		}
		D2Photon photon;
		photon.ray_index = seg.ray_index;
		photon.begin = seg.begin;
		photon.march = seg.march;
		photon.radiance = seg.radiance;
		 
		b_photon[index] = photon;
	}


}
