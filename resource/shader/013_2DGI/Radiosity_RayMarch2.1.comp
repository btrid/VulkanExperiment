#version 450
#extension GL_GOOGLE_include_directive : require

#define USE_GI2D 0
#define USE_GI2D_Radiosity 1
#include "GI2D.glsl"

#define HALF_PI (1.5707963)
#define TWO_PI (6.28318530)
#define QUARTER_PI (0.7853981633)

layout (local_size_x=128) in;

void main()
{
	const ivec4 reso = u_gi2d_info.m_resolution;

	uint ray_index = gl_GlobalInvocationID.x + u_radiosity_info.ray_frame_max*u_gi2d_scene.m_frame;
	D2Ray ray = b_ray[ray_index];
	if(ray.march==0){ return; }
	vec2 dir = calcDir(ray.angle);
	vec2 inv_dir = abs(1./dir);

	vec2 cell_origin = vec2(greaterThanEqual(dir, vec2(0.))) * vec2(8.);
	uint angle_index = ray_index / 1024;

	int begin = 0;
	int march = 0;

	int segment_index = atomicAdd(b_segment_counter.w, 1);
	if(segment_index%1024==0){
		atomicAdd(b_segment_counter.x, 1);
	}

	bool state;
	{
		ivec2 map_index = ivec2(ray.origin);
		ivec2 cell = map_index>>3;
		vec2 pos_sub = vec2(ray.origin - vec2(cell << 3));
		ivec2 cell_sub = ivec2(pos_sub);

		uint64_t map = b_diffuse_map[getMemoryOrder(cell)];
		state = (map & (1ul<<(cell_sub.x+cell_sub.y*8))) != 0ul;
	}

	for(; march < ray.march; )
	{
		vec2 pos = fma(dir, float(march).xx, ray.origin);
		ivec2 map_index = ivec2(pos);
		ivec2 cell = map_index>>3;
		vec2 pos_sub = vec2(pos - vec2(cell << 3));

		vec2 tp = fma(vec2(abs(cell_origin - pos_sub)), inv_dir, float(1.).xx);
		int skip = int(min(tp.x, tp.y));

		uint64_t map = b_diffuse_map[getMemoryOrder(cell)];
		if(map == (state?-1ul:0ul))
		{
			march+=skip;
			continue;
		}

		for(uint i = 0; i < skip; i++)
		{
			ivec2 cell_sub = ivec2(pos_sub);
			bool attr = (map & (1ul<<(cell_sub.x+cell_sub.y*8))) != 0ul;
			if(state != attr)
			{
				u16vec4 end = u16vec4(fma(dir.xyxy, vec4(float(begin).xx, float(march).xx), ray.origin.xyxy));
				b_segment_ex[segment_index] = end;

				uvec2 index = end.xz + end.yw * reso.xx;
				if(all(notEqual(end.xyxy, uvec4(0, 0, reso.xy-1))))
				{
					b_vertex_array[b_vertex_array_index[index.x]].vertex[angle_index] = end.zw;
				}
				if(all(notEqual(end.zwzw, uvec4(0, 0, reso.xy-1))))
				{
					b_vertex_array[b_vertex_array_index[index.y]].vertex[angle_index] = end.xy;
				}

				state = attr;
				begin = march;

				segment_index = atomicAdd(b_segment_counter.w, 1);
				if(segment_index%1024==0){
					atomicAdd(b_segment_counter.x, 1);
				}
			}
			march++;
			pos_sub = fma(dir, vec2(1.), pos_sub);
		}

	}
	u16vec4 end = u16vec4(fma(dir.xyxy, vec4(float(begin).xx, float(ray.march).xx), ray.origin.xyxy));
	b_segment_ex[segment_index] = end;
	uvec2 index = end.xz + end.yw * reso.xx;
	if(all(notEqual(end.xyxy, uvec4(0, 0, reso.xy-1))))
	{
		b_vertex_array[b_vertex_array_index[index.x]].vertex[angle_index] = end.zw;
	}
	if(all(notEqual(end.zwzw, uvec4(0, 0, reso.xy-1))))
	{
		b_vertex_array[b_vertex_array_index[index.y]].vertex[angle_index] = end.xy;
	}
}
