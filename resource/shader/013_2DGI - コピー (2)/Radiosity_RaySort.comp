#version 450
#extension GL_GOOGLE_include_directive : require

#include "btrlib/ConvertDimension.glsl"
#include "btrlib/Common.glsl"
#include "btrlib/Math.glsl"

#define USE_GI2D 0
#define USE_GI2D_Radiosity 1
#include "GI2D.glsl"

layout (local_size_x = 1024) in;

layout(push_constant) uniform Input
{
	int compare_bit;
	int distance;
} constant;

bool compare(in D2Ray a, in D2Ray b){
	if(a.angle != b.angle)
	{
		 return a.angle < b.angle;
	}
	ivec2 apos = ivec2(ceil(a.origin));
	ivec2 bpos = ivec2(ceil(b.origin));
	if(apos.y != bpos.y)
	{
		 return apos.y < bpos.y;
	}
	if(apos.x != bpos.x)
	{
		 return apos.x < bpos.x;
	}

	return a.march < b.march;
}


void main() 
{
	// http://http.developer.nvidia.com/GPUGems2/gpugems2_chapter46.html
	// http://t-pot.com/program/90_BitonicSort/index.html
	// https://gist.github.com/mre/1392067
	uint index_a = gl_GlobalInvocationID.x;
	uint index_b = index_a^constant.distance;

	// 自分より上のものだけチェックする
	if (index_a<index_b)
	{
		return;
	}

	// フレームを超えてはソートしない
	uint frame_a = index_a / u_radiosity_info.ray_frame_max;
	uint frame_b = index_b / u_radiosity_info.ray_frame_max;

	if(frame_a != frame_b)
	{
		return;
	}

	D2Ray a = b_ray[index_a];
	D2Ray b = b_ray[index_b];
	bool sort_dir = (index_a&constant.compare_bit)==0;
	if ( (sort_dir == compare(a,b))
	/*|| (sort_dir == (frame_a<frame_b))*/)
	{
		b_ray[index_a] = b;
		b_ray[index_b] = a;
	}
}
