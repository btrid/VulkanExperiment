#version 460
#extension GL_GOOGLE_include_directive : require

#include "Sky.glsl"

layout (local_size_x = 32, local_size_y = 32) in;
layout(push_constant) uniform Input
{
	float time;
} constant;

float band(in float start, in float peak, in float end, in float t)
{
	return smoothstep (start, peak, t) * (1. - smoothstep (peak, end, t));
}
float band2(in float ls, in float le, in float hs, in float he, in float t)
{
	return smoothstep (ls, le, t) * (1. - smoothstep (hs, he, t));
}

float intersectRaySphere(in vec3 origin, in vec3 ray, in vec4 sphere)
{
	vec3 relative_pos = sphere.xyz - origin;
	float tca = dot(relative_pos, ray);
//	if (tca < 0.) return -1.;

	float radiusSq = sphere.w * sphere.w;
	float d2 = dot(relative_pos, relative_pos) - tca * tca;
	if (d2 > radiusSq)
		return -1.;

	float thc = sqrt(radiusSq - d2);
	float t0 = tca - thc;
	float t1 = tca + thc;
	if (t0 < 0.) t0 = t1;
	return t0;
}

vec4 rand4(in vec3 co)
{
	vec4 value;
	value.x = dot(co.xyz, vec3(12.98, 25.23, 45.41));
	value.y = dot(co.yzx, vec3(32.89, 19.31, 29.11));
	value.z = dot(co.zxy, vec3(29.55, 13.94, 31.45));
	value.w = dot(co.yxz, vec3(65.14, 33.57, 12.12));

	return fract(sin(value * vec4(21.55, 78.23, 45.41, 31.37)) * 43.57);
}
vec4 noise4(in vec3 pos)
{
	vec3 ip = floor(pos);
	vec3 fp = smoothstep(0., 1., fract(pos));
	vec2 offset = vec2(0., 1.);
	vec4 aa = rand4(ip+offset.xxx);
	vec4 ab = rand4(ip+offset.xxy);
	vec4 ba = rand4(ip+offset.xyx);
	vec4 bb = rand4(ip+offset.xyy);
	vec4 v0 = mix(mix(aa, ab, fp.z), mix(ba, bb, fp.z), fp.y);

	aa = rand4(ip+offset.yxx);
	ab = rand4(ip+offset.yxy);
	ba = rand4(ip+offset.yyx);
	bb = rand4(ip+offset.yyy);

	return mix(v0, mix(mix(aa, ab, fp.z), mix(ba, bb, fp.z), fp.y), fp.x);
}
vec4 fBM4(in vec3 pos)
{
	float lacunarity = 2.5;
	float total = 0.;
	vec4 value = vec4(0.);
	for(int i = 0; i < 3; i++)
	{
		pos *= lacunarity;
		value = value*2. + noise4(pos);
		total = total*2. + 1.;
	}

	return value / total;
}

float cloud_density(in vec3 pos)
{
	vec3 p = pos * vec3(0.01,0.,0.01);

	vec4 map = fBM4(p + vec3(0., 0., constant.time*0.2));

	float coverage = 0.5;
	float alpha = 0.05;

	map = map * smoothstep(vec4(coverage, 0.,0.,0.), vec4(coverage+alpha, 0.,0.,0.), map);

	float t = (distance(pos, u_cloud_inner.xyz)-u_cloud_inner.w) * u_cloud_area_inv;
	float density = map.x * band(map.z, map.z+map.y*0.5, map.z+map.y*1., t+map.w*0.5);
	return density;
}

float cloud_lighting(in vec3 pos)
{
	float transparent = 1.;
	const int steps = 1;
	for (int i = 0; i < steps; i++) 
	{
		pos = pos + uLightRay * 1.;
		float density = cloud_density(pos);

		transparent *= exp(-density * ABSORPTION);
	}
	return transparent;
}

bool intersectRayAtom(vec3 Pos, vec3 Dir, vec3 AtomPos, vec2 Area, out vec4 OutDist)
{
	vec3 RelativePos = AtomPos - Pos;
	float tca = dot(RelativePos, Dir);

	vec2 RadiusSq = Area * Area;
	float d2 = dot(RelativePos, RelativePos) - tca * tca;

	OutDist = vec4(tca) + vec4(sqrt(RadiusSq.yxxy - d2)) * vec4(-1., -1., 1., 1.);
	return d2 <= RadiusSq.y;
}
vec4 cloud(in vec3 rayPos, in vec3 rayDir, float depth)
{
	vec4 dist;
	if(!intersectRayAtom(rayPos, rayDir, u_cloud_inner.xyz, vec2(u_cloud_inner.w, u_cloud_outer.w), dist))
	{
		// 雲にヒットしない
		return vec4(0.);
	}
	

	int count = 0;
	vec2 ray_segment[2];
	if(dist.y>0.)
	{
		// 雲の上から雲を見る
		ray_segment[count] = vec2(max(dist.x, 0.), dist.y-max(dist.x, 0.));
		count++;
	}
	if(dist.w>0. && depth<0.)
	{
		// 雲の下から雲を見る
		ray_segment[count] = vec2(max(dist.z, 0.), dist.w-max(dist.z, 0.));
		count++;
	}
#define STEP_LENGTH	1.
	float transparent = 1.;
	float alpha = 0.;
	vec3 color = vec3(0.);
	for(int i = 0; i <count; i++)
	{
		int step_num = int(ray_segment[i].y / STEP_LENGTH);
		float ray_step = ray_segment[i].y / float(step_num);
		vec3 pos = rayPos + rayDir * (ray_segment[i].x+ray_step*0.5);
		for(int step = 0; step < step_num; step++)
		{
			float density = cloud_density(pos);

			float t = exp(-density * ABSORPTION);
			transparent *= t;
			if (transparent < 0.01) { i = count; break; }

			color += transparent * density * cloud_lighting(pos);
			alpha += (1.-t) *(1.-alpha);

			pos = pos + rayDir * ray_step;

		}

	}
	return vec4(color, min(alpha, 1.));
}

void main()
{
	vec3 CamPos = vec3(0., 1., 0.);
	vec3 CamDir = normalize(vec3(1., 0., 0.));
	// カメラレイの作成
	{
		uvec3 reso = gl_NumWorkGroups * gl_WorkGroupSize;
		vec2 ndc = (vec2(gl_GlobalInvocationID.xy) / vec2(reso.xy)) * 2. -1.;
		vec3 up = vec3(0., 1., 0.); 
		vec3 side = normalize(cross(CamDir, normalize(up)));
		up = normalize(cross(CamDir, side));

		float CamFov = (radians(45.0));
		vec2 aspect = vec2(float(reso.x) / float(reso.y), 1);
		CamDir = normalize(side*ndc.x + up*ndc.y + CamDir);
	}

	vec3 sky_color = mix(vec3(.0, 0.1, 0.4), vec3(0.3, 0.6, 0.8), 1.0 - CamDir.y);
	sky_color = mix(sky_color, uLightColor, smoothstep(0.999, 1.00, dot(uLightRay, CamDir)));


	// 地面判定
	float distance_land = intersectRaySphere(CamPos, CamDir, u_planet);
	if(distance_land >= 0.)
	{
		// 地面に当たってる
		vec3 pos = CamPos + CamDir * distance_land;
		vec2 pos_i = floor(pos * 0.5).xz;
		float p = float(mod(pos_i.x + pos_i.y, 2.));
		sky_color = mix(vec3(.6, .6, .6), vec3(.75, .75, .75), p);

		// shadow
/*		if(true)
		{
			#define SHADOW_RAY_STEP 12
			#define SHADOW_RAY_LENGTH 5.
			float d1 = intersectRaySphere(pos, uLightRay, u_cloud_inner);
			float d2 = intersectRaySphere(pos, uLightRay, u_cloud_outer);
			int step_num = min(int((max(d2, 0.)-max(d1, 0.)) / float(SHADOW_RAY_LENGTH)), SHADOW_RAY_STEP);
			pos = pos + uLightRay * (d1 + SHADOW_RAY_LENGTH*0.5);
			vec3 ray = uLightRay/uLightRay.y;

			float transparent = 1.;
			for(int step = 0; step < step_num; step++)
			{
				float density = cloud_density(pos);

				float t = exp(-density * ABSORPTION);
				transparent *= t;
				if (transparent < 0.01) break;

				pos += ray * SHADOW_RAY_LENGTH; 
			}
			sky_color = mix(sky_color, vec3(0.), 1.-transparent);
		}
*/		
	}

	vec4 color = cloud(CamPos, CamDir, distance_land);

	color.xyz = mix(sky_color, color.xyz/(color.a+0.0001), color.a);

	imageStore(i_render_target, ivec2(gl_GlobalInvocationID.xy), vec4(color.xyz, 1.));

}